[main]
		
			GENERATE		X$MED1,FN$POISSON		; GENTE QUE ENTRA POR LA PUERTA 1 CON DISTR POISSON DE MEDIA X1
			TRANSFER		,INICIALIZAR

			GENERATE		X$MED2,FN$POISSON,5400	; GENTE QUE ENTRA POR LA PUERTA 2 CON DISTR POISSON DE MEDIA X2
			TRANSFER		,INICIALIZAR			; PERO EMPEZANDO A LAS 9:30 (1 HORA Y MEDIA)

											; A CADA UNO SE LE ASIGNA 
INICIALIZAR	ASSIGN		TIPO,FN$TIPOCLI			; UN TIPO
			ASSIGN		PROBIRSE,FN$PORCENTSEVA	; LA PROB DE IRSE SEGUN EL TIPO
			ASSIGN		MEDDEMORA,FN$MEDIADEMORA	; LA MEDIA DE DEMORA DEL TRAMITE
			ASSIGN		DESVDEMORA,FN$DESVIODEMORA	; EL DESVIO DE DEMORA DEL TRAMITE
			TABULATE 		TIPOS					; TABULO PARA VER SI LA DISTRIBUCION DE TIPOS FUE CORRECTA

EMPEZAR		ADVANCE		25,15					; TARDAN 25+/-15 SEG EN LLEGAR AL PUESTO DE ATENCION
			QUEUE			PATENCION
			SEIZE			PATENCION
			DEPART		PATENCION
			ADVANCE		30,15					; EL TIPO DEL PUESTO TARDA 30+/-15 SEG EN ATENDERLOS
			RELEASE		PATENCION
			
			TRANSFER		*PROBIRSE,IRSE			; UN PORCENTAJE SE VA SEGUN EL TIPO
			TRANSFER		,FN$DERIVAR			; DERIVA A LOS CLIENTES DE TIPO 1,2 A SERVICIO TECNICO 
											; Y A LOS DE 3,4 A PROMOCION Y VENTAS
ASERVTECNICO	ADVANCE		45,15					; TARDAN 45+/-15 SEG EN LLEGAR AL SECTOR DE SERVICIO TECNICO
			QUEUE			ATENCSERVTEC			; 5 TECNICOS ATIENDEN A TODOS
			ENTER			ATENCSERVTEC
			DEPART		ATENCSERVTEC
			ADVANCE		*MEDDEMORA,*DESVDEMORA	; TARDAN SEGUN SU TIPO LA MEDIA Y DESVIO CALCULADOS ANTERIORMENTE
			LEAVE			ATENCSERVTEC
			TRANSFER		,AELEGIRCAJA

APROMYVTAS		ADVANCE		60,30					; TARDAN 60+/-30 SEG EN LLEGAR AL SECTOR DE PROMOCION DE VENTAS
			QUEUE			ATENCPROMYVTAS			; 5 VENDEDORES ATIENDEN A TODOS
			ENTER			ATENCPROMYVTAS
			DEPART		ATENCPROMYVTAS
			ADVANCE		*MEDDEMORA,*DESVDEMORA	; TARDAN SEGUN SU TIPO LA MEDIA Y DESVIO CALCULADOS ANTERIORMENTE
			LEAVE			ATENCPROMYVTAS
			TRANSFER		,AELEGIRCAJA

; AL VOLVER, TODOS VAN A PASAR POR CAJA ANTES DE SALIR
AELEGIRCAJA	ADVANCE		45,15							; TARDAN 45+/-15 SEG EN LLEGAR A LAS CAJAS
			SELECT NU		CAJA,X$MINCAJAS,X$MAXCAJAS,,,OCUP	; ELEGIR LA PRIMER CAJA VACIA	
			TRANSFER		,ACAJA
OCUP			SELECT MIN		CAJA,X$MINCAJAS,X$MAXCAJAS,,Q			
ACAJA			QUEUE			CUENTAGENTE					; PARA CONTAR LA GENTE TOTAL EN LAS 5 COLAS
			QUEUE			*CAJA
			SEIZE			*CAJA
			DEPART		CUENTAGENTE
			DEPART		*CAJA
			ADVANCE		90,45					; TARDAN 90+/-45 SEG EN PAGAR/FIRMAR CONTRATO
			RELEASE		*CAJA
			
			TABULATE		TIEMPOENSISTEMA			; TABULO EL TIEMPO EN SISTEMA ANTES DE SALIR
IRSE			TERMINATE
			
;			GENERATE		30,20					; PARA TESTEAR COMO SE LLENAN LAS COLAS DE LA CAJA
;			TRANSFER		,AELEGIRCAJA
	





[timer]
; VARIACION DE LOS PARAMETROS EN FUNCION DEL TIEMPO
		INITIAL			X$MED1,90		; VALORES INICIALES
		INITIAL			X$MED2,0
		INITIAL			X$MAXCAJAS,12
		INITIAL			X$MINCAJAS,10

		GENERATE			,,5400,1		; A LAS 9:30 CAMBIAN LOS VALORES (1 HORA Y MEDIA)
		SAVEVALUE			MED1,60			
		SAVEVALUE			MED2,120
		SAVEVALUE			MAXCAJAS,13

		ADVANCE			7200,0		; A LAS 11:30 CAMBIAN LOS VALORES (2 HORAS)
		SAVEVALUE			MED1,45			
		SAVEVALUE			MED2,75
		SAVEVALUE			MAXCAJAS,14

		ADVANCE			5400,0		; A LAS 13:00 CAMBIAN LOS VALORES (1 HORA Y MEDIA)
		SAVEVALUE			MED1,40			
		SAVEVALUE			MED2,45
		SAVEVALUE			MAXCAJAS,15
		TERMINATE

; TABULACION DE GENTE EN CAJAS
		GENERATE			120,0,7200			; CADA 2 MIN A PARTIR DE LAS 10:00 (2 HS)
		TABULATE			GENTEENCOLACAJAS	; TABULO CUANTA GENTE ESTA EN LAS COLAS PARA LA CAJA
		TERMINATE

; TIEMPO DE SIMULACION
		GENERATE			3600,0	; GENERO UN TOKEN POR HORA
		TERMINATE			1
		;START 7					; SIMULO 7 HORAS (DE 8 A 15 HS)


[definiciones]
TIEMPOENSISTEMA		TABLE			M1,250,250,10		; TABLA PARA EL TIEMPO EN SISTEMA		
TIEMPOENCOLAATENCION	QTABLE		PATENCION,100,100,25	; TABLA PARA TIEMPO EN PRIMERA COLA
GENTEENCOLACAJAS	TABLE			Q$CUENTAGENTE,1,1,10	; TABLA PARA GENTE EN COLAS DE CAJA
TIPOS				TABLE			*TIPO,1,1,5		; TABLA PARA VER SI LA DISTRIBUCION DE TIPOS FUE CORRECTA

POISSON		FUNCTION		RN2,C24		; Función exponencial - POISSON (valor medio=1)
0.0,0.0/0.1,.104/.2,.222/.3,.335/.4,.509/.5,.69/.6,.915/.7,1.2/.75,1.38/.8,1.6/.84,1.83/.88,2.12/
0.9,2.3/.92,2.52/.94,2.81/.95,2.99/.96,3.2/.97,3.5/.98,3.9/.99,4.6/.995,5.3/.998,6.2/.999,7.0/.9997,8.0

TIPOCLI		FUNCTION		RN3,D4		; TIPO DE CLIENTE CON LAS PROBABILIDADES INDICADAS  
.35,1/.55,2/.7,3/1,4						; (35%->1,20%->2,15%->3,30%->4)

PORCENTSEVA	FUNCTION		*TIPO,D4		; PORCENTAJE QUE SE VA SEGUN EL TIPO (GUARDADO EN ATRIBUTO TIPO)
1,200/2,500/3,350/4,250						; (1->20%,2->50%,3->35%,4->25%)

MEDIADEMORA	FUNCTION		*TIPO,D4		; MEDIA DE LA DEMORA SEGUN EL TIPO (GUARDADO EN ATRIBUTO TIPO)
1,360/2,300/3,420/4,480					; (1->6 MIN,2->5 MIN,3->7 MIN,4->8 MIN)
	
DESVIODEMORA	FUNCTION		*TIPO,D4		; DESVIO DE LA DEMORA SEGUN EL TIPO (GUARDADO EN ATRIBUTO TIPO)
1,120/2,120/3,180/4,240					; (1->2 MIN,2->2 MIN,3->3 MIN,4->4 MIN)
	
DERIVAR		FUNCTION		*TIPO,D2		; DERIVA A LOS CLIENTES DE TIPO 1,2 A SERVICIO TECNICO 
2,ASERVTECNICO/4,APROMYVTAS 				; Y A LOS DE 3,4 A PROMOCION Y VENTAS	

ATENCSERVTEC	STORAGE		5			; 5 TECNICOS QUE ATIENDEN EN EL AREA DE SERVICIO TECNICO
ATENCPROMYVTAS	STORAGE		6			; 6 VENDEDORES QUE ATIENDEN EN EL AREA DE SERVICIO TECNICO


<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
