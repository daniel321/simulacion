[main]
		GENERATE	X$MED1,FN$POISSON
		TRANSFER	,INICIO
		GENERATE	X$MED2,FN$POISSON,5400
		TRANSFER	,INICIO
INICIO	ASSIGN	TIPO,FN$TIPOCLI
		ASSIGN	DESV,FN$DESVDEMORA	
		TABULATE	DISTTIPOS			;PARA TESTING
		ADVANCE	25,15
		QUEUE		ATENC
		ENTER		ATENC
		DEPART	ATENC
		ADVANCE	30,15
		LEAVE		ATENC
		
		TRANSFER	FN$PCENTSEVA,,FIN	
		
		TEST	LE	*TIPO,2,AVTAS
ASVTECNICO	ADVANCE	45,15
		QUEUE		ATENCSERVTEC
		ENTER		ATENCSERVTEC
		DEPART	ATENCSERVTEC
		ADVANCE	FN$MEDIADEMORA,*DESV
		LEAVE		ATENCSERVTEC
		TRANSFER	,ACAJAS
AVTAS		ADVANCE	60,30
		QUEUE		ATENCPROMVTAS
		ENTER		ATENCPROMVTAS
		DEPART	ATENCPROMVTAS
		ADVANCE	FN$MEDIADEMORA,*DESV
		LEAVE		ATENCPROMVTAS
		TRANSFER	,ACAJAS
ACAJAS	ADVANCE	45,15
		SELECT NU	CAJA,1,X$CANTCAJAS,,,OCUP
		TRANSFER	,ACOLACAJAS
OCUP		SELECT MIN	CAJA,1,X$CANTCAJAS,,Q
ACOLACAJAS	QUEUE		CAJAS
		QUEUE		*CAJA
		SEIZE		*CAJA
		DEPART	*CAJA
		DEPART	CAJAS
		ADVANCE	90,45
		RELEASE	*CAJA
FIN		TABULATE	TTIEMPOSIST
		TERMINATE


[declaraciones]
TGENTECOLASCAJA		TABLE		Q$CAJAS,1,1,20
TTIEMPOSIST		TABLE		M1,180,180,30	
TTIEMPOCOLAATENC	QTABLE	ATENC,5,60,30
TGENTECOLASCAJA1	TABLE		Q1,1,1,20	; PARA TESTING
TGENTECOLASCAJA2	TABLE		Q2,1,1,20
TGENTECOLASCAJA3	TABLE		Q3,1,1,20
TGENTECOLASCAJA4	TABLE		Q4,1,1,20
TGENTECOLASCAJA5	TABLE		Q5,1,1,20
DISTTIPOS			TABLE		*TIPO,1,1,5
	
POISSON			FUNCTION		RN2,C24	
0.0,0.0/0.1,.104/.2,.222/.3,.335/.4,.509/.5,.69/.6,.915/.7,1.2/.75,1.38/.8,1.6/.84,1.83/.88,2.12/
0.9,2.3/.92,2.52/.94,2.81/.95,2.99/.96,3.2/.97,3.5/.98,3.9/.99,4.6/.995,5.3/.998,6.2/.999,7.0/.9997,8.0
TIPOCLI			FUNCTION		RN3,D4
.35,1/.55,2/.70,3/1,4
PCENTSEVA			FUNCTION		*TIPO,D4
1,200/2,500/3,350/4,250
MEDIADEMORA		FUNCTION		*TIPO,D4
1,360/2,300/3,420/4,480
DESVDEMORA			FUNCTION		*TIPO,D4
1,120/2,120/3,180/4,240

ATENCSERVTEC		STORAGE		5
ATENCPROMVTAS		STORAGE		6
ATENC				STORAGE		2

[time]
		;MEDIA DE ARRIBO Y CAJAS A LO LARGO DEL DIA 
		INITIAL	X$MED1,90
		INITIAL	X$MED2,120
		INITIAL	X$CANTCAJAS,1
		GENERATE	,,5400,1
		SAVEVALUE	MED1,60
		SAVEVALUE	CANTCAJAS,2
		ADVANCE	7200,0
		SAVEVALUE	MED1,45
		SAVEVALUE	MED2,75
		SAVEVALUE	CANTCAJAS,3
		ADVANCE	5400,0
		SAVEVALUE	MED1,40
		SAVEVALUE	MED2,45	
		TERMINATE
		;TABULAR GENTE EN COLAS CADA 2 MIN A PARTIR DE LAS 10
		GENERATE	120,0,7200	
		TABULATE	TGENTECOLASCAJA
		TABULATE	TGENTECOLASCAJA1		;PARA TESTING
		TABULATE	TGENTECOLASCAJA2
		TABULATE	TGENTECOLASCAJA3
		TABULATE	TGENTECOLASCAJA4
		TABULATE	TGENTECOLASCAJA5
		TERMINATE
		;SIMULO DE 8 A 15
		GENERATE	3600,0
		TERMINATE	1
		;START	7


<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
